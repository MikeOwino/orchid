buildscript {
    ext.kotlin_version = '1.3.61'
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

rootProject.buildDir = "${orcOutput}/build"

subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def flutter = files(orcFlutter)

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId orcUnique
        minSdkVersion 21
        targetSdkVersion 31
        versionCode orcMonotonic.toInteger() - 946684800
        versionName orcVersion
        manifestPlaceholders = [orcUnique: orcUnique, orcName: orcName]
    }

    signingConfigs {
        orchid {
            keyAlias orcAlias
            keyPassword orcPassword
            storeFile file(orcStore)
            storePassword orcPassword
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.orchid
        }

        release {
            signingConfig signingConfigs.orchid
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    sourceSets {
        main {
            java.srcDirs += [orcInterface + "/android/app/src/main/java"]
            res.srcDirs += [orcInterface + "/res"]
            assets.srcDirs += [orcOutput + "/assets"]
            jniLibs.srcDirs = [orcOutput + "/jnilibs"]
        }
    }

    dependencies {
        implementation flutter
        implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

        // https://github.com/flutter/flutter/issues/76183
        // this library is now used by a35e3fe4a34b94a9b624f56e81ad4ff040230a40
        // but, flutter fails to add a dependency to ensure it gets embedded :/
        implementation "androidx.activity:activity:1.0.0"

        // XXX: upgrading to Flutter 3.3 caused these to be required somehow :/
        implementation "androidx.core:core:1.6.0"
        implementation "androidx.tracing:tracing:1.0.0"
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

import groovy.json.JsonSlurper
def plugins = new JsonSlurper().parseText(file(orcInterface + "/.flutter-plugins-dependencies").text)
plugins.plugins.android.each { dependency ->
    def plugin = project.rootProject.findProject(":${dependency.name}")

    project.dependencies {
        implementation plugin
    }

    plugin.afterEvaluate {
        plugin.android.buildTypes {
            profile {
                initWith debug
            }
        }
        plugin.dependencies {
            compileOnly flutter

            // https://github.com/flutter/flutter/issues/56686
            // WakelockPlugin.kt: Unresolved reference: NonNull
            implementation "androidx.annotation:annotation:1.0.0"
        }
    }
}
